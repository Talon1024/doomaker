// Line comments (// to end of line)
not_newline  = @{ !NEWLINE ~ ANY }
line_comment = @{ "//" ~ not_newline* ~ NEWLINE }
// Block comments (/* to first */, not nestable)
not_end_block = _{ !"*/" ~ ANY }
block_comment = _{ "/*" ~ not_end_block* ~ "*/" }
// All comments
COMMENT = _{ line_comment | block_comment }

// Common
WHITESPACE = _{ (" " | "\t" | NEWLINE) }

// Strings
text_piece = @{ "\"" ~ text_chars ~ "\"" }
text_chars = @{ (!("\"" | NEWLINE) ~ ("\\\"" | ANY))* }

// Names (single-quoted strings)
name_piece = @{ "'" ~ name_chars ~ "'" }
name_chars = @{ (!("'" | NEWLINE) ~ ("\\'" | ANY))* }

// Numbers
basic_number = @{ ASCII_DIGIT+ }
hex_number   = @{ (ASCII_DIGIT | 'a'..'f' | 'A'..'F')+ }
exponent     = @{ ("e" | "E") ~ ("-" | "+"?) ~ basic_number }

// Integers
// See https://github.com/ZDoom/gzdoom/blob/d2ac72129df5dbf93b185b54ac259436cd71a440/src/common/engine/sc_man_scanner.re#L234
int_suffix  = @{ ("l" | "L" | "u" | "U"){1, 2} }
integer_dec = @{ "-"? ~ basic_number ~ int_suffix? }
integer_oct = @{ "-"? ~ "0" ~ basic_number ~ int_suffix? }
integer_hex = @{ "-"? ~ "0x" ~ hex_number ~ int_suffix? }
integer     = @{ integer_hex | integer_oct | integer_dec }

// Floats/decimals
// See https://github.com/ZDoom/gzdoom/blob/d2ac72129df5dbf93b185b54ac259436cd71a440/src/common/engine/sc_man_scanner.re#L237
float_suffix        = @{ "f" | "F" }
decimal_fraction    = @{ "-"? ~ "." ~ basic_number ~ exponent? ~ float_suffix? }
decimal_integer     = @{ "-"? ~ basic_number ~ exponent ~ float_suffix? }
decimal_with_prefix = @{ "-"? ~ basic_number ~ "." ~ basic_number? ~ exponent? ~ float_suffix? }
decimal             = @{ decimal_with_prefix | decimal_integer | decimal_fraction }

// Booleans
boolean = @{ "true" | "false" }

// Any data type
any_data = { text_piece | decimal | integer | boolean }

identifier = @{ (ASCII_ALPHANUMERIC | "_")+ }
